
Day13 = (
    | dots instrs |

    setup = (
        | input |
        input := system loadFile: 'inputs/day13.txt'.
        input isNil ifTrue: [ self error: 'could not open input file' ].

        input := input split: '\n\n'.
        input := input reject: [ :it | it length = 0 ].
        input := input asArray.

        dots := input at: 1.
        dots := dots split: '\n'.
        dots := dots collect: [ :line |
            | parts |
            parts := line split: ','.
            Point fromX: (parts at: 1) asInteger andY: (parts at: 2) asInteger
        ].

        instrs := input at: 2.
        instrs := instrs split: '\n'.
        instrs := instrs reject: [ :it | it length = 0 ].
        instrs := instrs asArray.
        instrs := instrs collect: [ :line |
            | parts axis |
            parts := line split: '='.
            axis := parts at: 1.
            axis := axis charAt: axis length.
            Array with: axis asSymbol with: (parts at: 2) asInteger.
        ].
    )

    " deduplicates an array by passing its items through a set "
    dedup: array = (
        | set idx |
        set := Set new.
        set addAll: array.
        array := Array new: set size.
        idx := 1.
        set do: [ :it |
            array at: idx put: it.
            idx := idx + 1.
        ].
        ^ array.
    )

    " fold the given dots along the given value on the X axis "
    foldX: dots at: x = (
        dots do: [ :dot |
            dot getX > x ifTrue: [ dot setX: x - (dot getX - x) ].
        ].
        ^ self dedup: dots.
    )

    " fold the given dots along the given value on the Y axis "
    foldY: dots at: y = (
        dots do: [ :dot |
            dot getY > y ifTrue: [ dot setY: y - (dot getY - y) ].
        ].
        ^ self dedup: dots.
    )

    " fold the given dots along a given axis "
    fold: grid along: axis at: value = (
        axis = #x ifTrue: [ ^ self foldX: grid at: value ].
        axis = #y ifTrue: [ ^ self foldY: grid at: value ].
    )

    part1 = (
        | answer input instr |

        instr := instrs at: 1.
        input := dots collect: [ :it | it clone ].
        answer := self fold: input along: (instr at: 1) at: (instr at: 2).

        'part1: ' print. answer length println.
    )

    part2 = (
        | answer input bounds |

        input := dots collect: [ :it | it clone ].
        answer := instrs inject: input into: [ :acc :instr |
            self fold: input along: (instr at: 1) at: (instr at: 2).
        ].

        bounds := answer inject: (Point fromX: 0 andY: 0) into: [ :acc :it |
            acc setX: (acc getX max: it getX).
            acc setY: (acc getY max: it getY).
            acc
        ].

        'part2:' println.

        0 to: bounds getY do: [ :y |
            0 to: bounds getX do: [ :x |
                | point |
                point := Point fromX: x andY: y.
                (answer contains: point) ifTrue: [
                    '#' print.
                ] ifFalse: [
                    ' ' print.
                ].
            ].
            '' println.
        ].
    )

    run: args = (
        self setup.
        Timing measure: [ self part1 ].
        Timing measure: [ self part2 ].
    )
)
